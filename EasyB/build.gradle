dependencies {
    testRuntime("org.easyb:easyb-core:1.6") {
        exclude(group: 'ant', module: 'ant')
        exclude(group: 'org.codehaus.groovy', module: 'groovy-all')
        exclude(group: 'org.easyb', module: 'easyb-composite-groovy')
    }
    compile "org.codehaus.groovy:groovy-all:${rootProject.ext.groovyVersion}"
    compile('net.sourceforge.htmlunit:htmlunit:2.26') {
        exclude(group: 'xml-apis', module: 'xml-apis')
    }
}

sourceSets {
    main {
        groovy {
            srcDir '../HtmlUnit/src/main/groovy'
            include 'BlogTester.groovy'
        }
    }
}

task easyb(dependsOn: compileGroovy) {
    doLast {
        ant.taskdef(name: "easyb", classname: "org.easyb.ant.BehaviorRunnerTask", classpath: sourceSets.test.runtimeClasspath.asPath)
        project.testResultsDir.mkdirs()
        ant.echo sourceSets.test.runtimeClasspath.asPath
        ant.easyb(failureProperty: 'easyb_failed') {
            classpath {
                pathElement(path: sourceSets.test.runtimeClasspath.asPath)
            }
            report(location: "${project.testResultsDir}/story.txt", format: "txtstory")
            report(location: "${project.testResultsDir}/index.html", format: "html")
            behaviors(dir: "src/test/stories") {
                include name: "**/*Story.groovy"
            }
        }
        ant.fail(if: 'easyb_failed', message: 'Failures in easyb stories')
    }
}
