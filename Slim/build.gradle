configurations {
    xebium
}

repositories {
    mavenCentral()
    mavenRepo name:'Xebia', urls: 'http://os.xebia.com/maven2/'
}

dependencies {
    testCompile('net.sourceforge.htmlunit:htmlunit:2.9') {
        exclude(group: 'xml-apis', module: 'xml-apis')
    }
    testCompile files("${projectDir}/fitnesse/fitnesse.jar")
    groovy "org.codehaus.groovy:groovy-all:1.8.3"
    groovy "junit:junit:4.10"
    xebium("com.xebia.incubator:xebium:0.5:jar-with-dependencies") { transitive = false }
    xebium("org.slf4j:slf4j-simple:1.6.2") { transitive = false }
}

task dist(type: Copy, dependsOn: compileTestGroovy) {
    into "$buildDir/libs"
    from configurations.testRuntime
}

task downloadXebium(type: Copy) {
    into "$projectDir/fitnesse"
    from configurations.xebium
}

task downloadHtmlFixture() {
    def version = '2.5.1'
    def name = "HtmlFixture-$version"
    def tempDir = System.getProperty('java.io.tmpdir')
    ant.get(src: "http://sourceforge.net/projects/htmlfixtureim/files/htmlfixtureim/$version/${name}.zip/download",
        dest: "${file(tempDir)}/${name}.zip")
    copy {
        from zipTree("$tempDir/${name}.zip")
        into 'fitnesse'
        excludes = ['**/fitnesse*.jar', '**/xml-apis*.jar']
        include "$name/lib/*.jar"
    }
}

fitnessePort = 9090

// similar to start: does show error logs but doesn't return
task startSync(dependsOn: dist) << {
    ant.echo(message: "Starting FitNesse: http://localhost:$fitnessePort/")
    ant.java(jar: "${projectDir}/fitnesse/fitnesse.jar",
            logError: true, fork: true, dir: "${projectDir}/fitnesse") {
        arg(line: "-p $fitnessePort")
    }
}

task start(dependsOn: dist) << {
    ant.java(jar: "${projectDir}/fitnesse/fitnesse.jar",
            spawn: true, fork: true, dir: "${projectDir}/fitnesse") {
        arg(line: "-p $fitnessePort")
    }
    ant.echo(message: "Started FitNesse: http://localhost:$fitnessePort/")
}

task stop() << {
    ant.java(classname: "fitnesse.Shutdown") {
        classpath(path: "${projectDir}/fitnesse/fitnesse.jar")
        arg(line: "-p $fitnessePort")
    }
}
